.MODEL SMALL
.STACK 100H

.DATA
tam_nombre          EQU 30                                      ; Tama√É¬±o m√É¬°ximo para el nombre del estudiante. 
tam_calif           EQU 10                                      ; Tama√É¬±o m√É¬°ximo para la calificaci√É¬≥n. 
max_estudiantes     EQU 15                                       ; N√É¬∫mero m√É¬°ximo de estudiantes en el registro.
contador            DB 0                                        ; Registro de estudiantes ingresados. 

; ---< Buffers >---
; [Tam m√É¬°ximo][Caracts le√É¬≠dos][Buffer]
; [0]: Tama√É¬±o m√É¬°ximo de la entrada.
; [1]: N√É¬∫mero real de caracteres le√É¬≠dos.
; [2]: Buffer de almaenamiento. 

buffer_nombre   DB tam_nombre                                   ; Almacenado tempral de nombres.
                DB ?  
                DB tam_nombre+2 DUP(0)

buffer_calif    DB tam_calif                                    ; Almacenado temporal de calificaciones. 
                DB ?
                DB tam_calif+2 DUP(0)

buffer_id       DB 3                                            ; Buffer para el ingreso de n√É¬∫meros puntuales (2 dig + ENTER).
                DB ?
                DB 3 DUP(0)

; ---< Listas/Arrays de almacenamiento >---
; Almacenado de caracteres. 
lista_nombres   DB max_estudiantes * (tam_nombre+1) DUP('$')    ; Rellenan con car√É¬°cter nulo '$'.
lista_califs    DB max_estudiantes * (tam_calif+1) DUP('$')

; Almacenda de resultados num√É¬©ricos. 
array_enteros   DW max_estudiantes dup(0)                       ; Parte entera de la calificaci√É¬≥n (16 bits).
array_decimales DW max_estudiantes * 2 dup(0)                   ; Parte fraccionaria de la calificaci√É¬≥n (32 bits).

; Variables temporales 
temp_entero     DW 0                                            ; Almacena temporalmente la parte entera durante el parsing. 
temp_decimal    DW 2 dup(0)                                     ; Almacena temporalmente la parte decimal durante el parsing.
flag_decimal    DB 0                                            ; Flag al encontrar el punto decimal. 
temp_index DB 0


; Variables para promedio
suma_total      DW 0
promedio_ponderado DW 0
  
; Contadores de aprobaciones
contador_aprobados      DB 0
contador_desaprobados   DB 0  
 
; Bubble Sort 
; Array que contiene indices resultantes de un Bubble Sort
array_BS   DW max_estudiantes dup(0)
orden            DB 1 ; ASC = 1, DSC = 0   
resultado_dec DB ?   ; 1 = primero mayor, 0 = segundo mayor, 2 = iguales 

; Buffers para datos ordenados
buffer_nombres_ordenados DB max_estudiantes * (tam_nombre + 1) DUP('$')
buffer_califs_ordenados  DB max_estudiantes * (tam_calif + 1) DUP('$')

; Variables temporales
temp_reg    DW 0      ; Para c√°lculos temporales
source_ptr  DW 0      ; Para guardar puntero origen

; ---< Mensajes y Prompts del Sistema >---
msgMenu     DB 13, 10, '-------------< CE1106 - SISTEMA DE REGISTRO >------------', 13, 10
            DB         '------------------< MENU PRINCIPAL >---------------------',  13, 10
            DB         '| 1. Agregar Estudiante y Calificacion                  |', 13, 10
            DB         '| 2. Mostrar Estadisticas                               |', 13, 10
            DB         '| 3. Buscar Estudiante por ID                           |', 13, 10 
            DB         '| 4. Ordenar Calificaciones (ASC/DESC)                  |', 13, 10
            DB         '| 5. Registros                                          |', 13, 10
            DB         '| 0. Salir del Programa                                 |', 13, 10
            DB         '---------------------------------------------------------', 13, 10
            DB         '                                                         ', 13, 10
            DB         'Ingrese opcion: $'

; Mensajes [1]
msgT1               DB 13, 10, '[ AGREGAR ESTUDIANTE ]', 13, 10, '$'
msgPedirNombre      DB 13, 10, 'Ingrese nombre completo del estudiante: $'
msgPedirNota        DB 13, 10, 'Ingrese la calificacion del estudiante: $'
msgCalifInvalida    DB 13, 10, 'Error! La calificacion no cuenta con el formato adecuado... Intente nuevamente...', 13, 10,'$'

; Mensajes [2]
msgT2               DB 13, 10, '[ ESTADISTICAS ]', 13, 10, '$'
msgDebug            DB 13, 10, 'Procesado: $' 
msgNoStats          DB 13, 10, 'No hay estudiantes registrados en el sistema...', 13, 10, '$'
msgPorcAprobados    DB 13, 10, 'Porcentaje de aprobados: $'
msgPorcReprobados   DB 13, 10, 'Porcentaje de reprobados: $'
msgMaxNota          DB 13, 10, 'El estudiante con mayor nota es: $'
msgMinNota          DB 13, 10, 'El estudiante con menor nota es: $'
msgMaxMin           DB 13, 10, 'Se presentaran datos de notas mayores y menores en orden de ID, Nombre y Nota.$'
msgPonderado        DB 13, 10, 'Promedio ponderado: $' ;(mostrar el mensaje de ponderado)
; Mensajes [3]
msgT3               DB 13, 10, '[ BUSQUEDA POR ID ]', 13, 10, '$'
msgPedirID          DB 13, 10, 'Ingrese el ID del estudiante: $'
msgIDInvalido       DB 13, 10, 'ID invalido o estudiante no existe!', 13, 10, '$'
msgEstEncontrado    DB 13, 10, 'Estudiante encontrado:', 13, 10, '$'  

; Mensajes [4]
msgT4               DB 13, 10, '[ ORDENAMIENTO DE CALIFICACIONES ]', 13, 10, '$'
msg_pregunta    DB 13,10,'Ingrese 1 para ASC o 0 para DSC: $'
msg_error       DB 13,10,'Error: Ingrese solo 1 o 0',13,10,'$'

; Mensajes [5]
msgT5               DB  13, 10, '[ REGISTROS ]', 13, 10, '$'
msgEncabezado       DB 'N.', 9,9,'Nombre', 9,9,'Calificacion$'
msgSeparador        DB 13, 10, '-----------------------------------------------', 13, 10, '$'
msgListaVacia       DB 13, 10, 'La lista se encuentre vacia...$'

; Mensajes [0]
msgDespedida        DB 13, 10, '-------------------------------------------------', 13, 10
                    DB         '           SISTEMA DE REGISTRO - CE1106          ', 13, 10
                    DB         '-------------------------------------------------', 13, 10
                    DB         '          Gracias por usar el sistema!           ', 13, 10
                    DB         '         Programa terminado correctamente. :D    ', 13, 10
                    DB         '-------------------------------------------------', 13, 10, '$'

; Formato
nueva_linea     DB 13, 10, '$'
tabulador       DB 09h, '$'

; Mensajes de estado y error/advertencias
msgOpcionInvalida   DB 13, 10, 'Opcion invalida! Presione cualquier tecla...$'
msgMaxAlcanzado     DB 13, 10, 'Maximo de estudiantes alcanzado!$'
msgContinuar        DB 13, 10, 'Presione cualquier tecla para continuar...$'
                                
.CODE
START:
    ; Inicializar segmentos de datos.
    MOV AX, @DATA
    MOV DS, AX                                                  ; Inicializar Data Segment.
    MOV ES, AX                                                  ; Inicializar Extra Segment.

MenuPrincipal:                                                  ; Bucle principal. 
    CALL LimpiarPantalla
    CALL MostrarMenu
    JMP MenuPrincipal

MostrarMenu PROC
    ; Mostrar Men√É¬∫ en pantalla. 
    MOV AH, 09h
    LEA DX, msgMenu
    INT 21h

    ; Leer opci√É¬≥n ingresada por el usuario. 
    MOV AH, 01h                                                  ; Funci√É¬≥n DOS: Leer car√É¬°cter desde teclado.
    INT 21h                                                      ; El car√É¬°cter ingresado se almacena en AL. 

    ; Procesado de opci√É¬≥n seleccionada.     
    CMP AL, '1'
    JE Opcion1                                                   ; Agregar estudidante y calificaci√É¬≥n.
    CMP AL, '2'
    JE Opcion2                                                   ; Mostrar estad√É¬≠stica. 
    CMP AL, '3'
    JE Opcion3                                                   ; Busqueda por ID.
    CMP AL, '4'
    JE Opcion4                                                   ; Ordenado por calificaci√É¬≥n. 
    CMP AL, '5'
    JE Opcion5                                                  ; Mostrar datos ingresados. 
    CMP AL, '0' 
    JE SalirPrograma                                            ; Salir del programa.
    CMP AL, 1Bh
    JE SalirPrograma                                            ; Salir del programa con ESC.
    
    ; Opci√É¬≥n inv√É¬°lida. 
    MOV AH, 09h
    LEA DX, msgOpcionInvalida
    INT 21h

    ; Esperar tecla para continuar.
    MOV AH, 01h
    INT 21h
    RET                                                           ; Retornar al men√É¬∫ principal
MostrarMenu ENDP

; ---< Opciones del Programa >---
Opcion1:
    CALL IntentarAgregar
    RET

Opcion2:
    CALL LimpiarPantalla
    CALL separar_numeros_func
    
    ; preparar registros para la rutina
    LEA BX, array_enteros     ; BX = base de enteros
    LEA SI, array_decimales   ; SI = base de decimales
    MOV CX, max_estudiantes   ; cantidad de elementos  
    
    ; array_BS almacena orden.
    CALL BUBBLE_SORT_INDICES
    
    CALL ordenarListas     

    CALL MostrarEstadisticas
    
                            
    MOV AH, 09h
    LEA DX, msgMaxMin
    INT 21h
                            
    CALL generar_Max_Min 
    
    
    JMP FinEstadisticas
    
    RET

Opcion3:
    CALL BusquedaPorID
    RET

Opcion4:
    CALL LimpiarPantalla
    CALL separar_numeros_func
    
    call PREGUNTAR_ORDEN 
    
    ; preparar registros para la rutina
    LEA BX, array_enteros     ; BX = base de enteros
    LEA SI, array_decimales   ; SI = base de decimales
    MOV CX, max_estudiantes   ; cantidad de elementos  
    
    ; array_BS almacena orden.
    CALL BUBBLE_SORT_INDICES
    
    CALL ordenarListas 
    CALL MostrarListaCompleta
        
     
    RET

Opcion5:
    CALL MostrarListaCompleta
    RET

SalirPrograma:
    CALL LimpiarPantalla

    MOV AH, 09h
    LEA DX, msgDespedida
    INT 21h

    MOV AH, 4Ch
    INT 21h

; ---< Agregar Estudiante y Calificacion >--- 
IntentarAgregar PROC
    PUSH AX

    CALL LimpiarPantalla

    ; T√≠tulo de la Secci√≥n.
    MOV AH, 09h
    LEA DX, msgT1
    INT 21h
    
    ; Verifica si hay espacio suficiente.
    MOV AL, contador
    CMP AL, max_estudiantes
    JL  ProcesarEntrada                                            ; S√≠ puede agregar estudiante (contador < max_estudiantes).
    
    ; Espacio insuficiente. 
    MOV AH, 09h
    LEA DX, msgMaxAlcanzado
    INT 21h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h

    ; Retorna a Men√∫ Principal.
    JMP FinAgregar
    
PuedeAgregar:
    CALL ProcesarEntrada
    
FinAgregar:
    POP AX
    RET

IntentarAgregar ENDP

ProcesarEntrada PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    PUSH DI                                                         ; Preservar los registros.

PedirNombre:
    ; Pedir nombre del estudiante. 
    MOV AH, 09h
    LEA DX, msgPedirNombre
    INT 21h
    
    ; Leer entrada del nombre. 
    MOV buffer_nombre, tam_nombre                                   ; Establecer tama√±o del buffer en [0].
    LEA DX, buffer_nombre                                           ; DX apunta al buffer de entrada. 
    MOV AH, 0Ah
    INT 21h
    
    ; Terminaci√≥n '$' para compatibilidad con DOS.
    XOR BX, BX                                                      ; Limpiar BX.
    MOV BL, buffer_nombre[1]                                        ; BL = n√∫mero de caracteres le√≠dos.
    MOV buffer_nombre[BX+2], '$'                                    ; Agregar '$'. 
    
    ; Calcular posici√≥n de destino. 
    XOR AX, AX
    MOV AL, contador
    MOV BL, tam_nombre+1                                            ; BL = tama√±o de cada entrada (incluye terminador).
    MUL BL                                                          ; AX (deslpazamiento) = contador * (tam_nombre+1)
    LEA DI, lista_nombres
    ADD DI, AX                                                      ; DI (destino en lista) = deslpazamiento + inicio 
    LEA SI, buffer_nombre+2                                         ; SI apunta a los datos en [2]
    CALL CopiarCadena                                               ; Copiar cadena en lista. 

PedirCalif:
    ; Pedir calificaci√≥n del estudiante.
    MOV AH, 09h
    LEA DX, msgPedirNota
    INT 21h

ValidarCalif:
    ; Leer entrada de calificaci√≥n. 
    MOV buffer_calif, tam_calif
    LEA DX, buffer_calif
    MOV AH, 0Ah
    INT 21h

    ; Validar formato num√©rico y formatear
    CALL FormatoCalif                                               ; Flag en caso de error.
    JC  ErrorCalif

    ; Corte para notas iguales o mayores a 100.
    CALL VerifCien

    JMP CalificacionValida
    
ErrorCalif:
    ; Mostrar mensaje de error y volver a pedir.
    MOV AH, 09h
    LEA DX, msgCalifInvalida
    INT 21h
    JMP PedirCalif  

CalificacionValida:
    ; Copiar calificaci√≥n en lista.
    XOR AX, AX
    MOV AL, contador
    MOV BL, tam_calif+1
    MUL BL
    LEA DI, lista_califs
    ADD DI, AX
    LEA SI, buffer_calif + 2
    CALL CopiarCadena

    ; Incrementar contador.
    INC contador
    
    ; Salto de l√≠nea. 
    MOV AH, 09h
    LEA DX, nueva_linea
    INT 21h
    
    POP DI
    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    JMP MenuPrincipal
ProcesarEntrada ENDP

FormatoCalif PROC                                                   ; Se asegura que la calificacion registrada en el buffer cumpla con el formato.
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    PUSH DI
    
    LEA SI, buffer_calif+2
    XOR DI, DI
    XOR BX, BX
    XOR CX, CX
    MOV DX, 10
    
ValidarLoop:
    MOV AL, [SI]
    
    ; Validaci√≥n de caracteres espaciales y pasar a formatear.
    CMP AL, '$'
    JE  IniciarFormato
    CMP AL, 13
    JE  IniciarFormato
    CMP AL, '.'
    JE  PuntoValido
    
    ; Validaci√≥n de d√≠gitos.
    CMP AL, '0'
    JL  ErrorFormato
    CMP AL, '9'
    JG  ErrorFormato
    
    ; Verificaci√≥n de parte decimal (m√°ximo 5 digitos).
    CMP DI, 1
    JNE SaltarValidacion
    
    ; Contador decimal.
    INC BX
    CMP BX, 5
    JG ErrorFormato

; Vuelve al loop con siguiente caracter. 
SaltarValidacion:
    INC SI
    JMP ValidarLoop

; Determina inicio de parte decimal y error si multiples puntos.
PuntoValido:
    CMP DI, 1
    JE  ErrorFormato
    MOV DI, 1

    ; Pasar a siguiente caracter.
    INC SI
    JMP ValidarLoop

; Error en el formato ingresada retornar error...
ErrorFormato:
    ; Set flag y salir. 
    STC
    JMP FinFormato

; Inicio del formateo de la parte decimal.
IniciarFormato:                                                      ; Se asegura de retonar una calificacion con formato decimal '.00000'
    LEA SI, buffer_calif+2
    XOR DI, DI
    XOR BX, BX
    
BuscarPunto:
    ; Iterar sobre cadena. 

    ; Casos para un a√±adido completo de parte decimal.
    MOV AL, [SI]
    CMP AL, '$'
    JE  AgregarPuntoYDecimales
    CMP AL, 13 
    JE  AgregarPuntoYDecimales

    ; Iteracion en busca de parte fraccionaria.
    CMP AL, '.'
    JE  EncontrarPunto
    INC SI
    JMP BuscarPunto

; Despu√©s de encontrado el punto, contar decimales. 
EncontrarPunto:
    MOV DI, 1
    INC SI
ContarDecimales:
    MOV AL, [SI]
    CMP AL, '$'
    JE  Rellenar
    CMP AL, 13
    JE  Rellenar
    INC BX
    INC SI
    JMP ContarDecimales

; Rellenado con ceros. 
Rellenar:
    ; Rellenado completo.
    CMP DI, 0
    JE  AgregarPuntoYDecimales
    
    ; Determinar ceros restantes.
    MOV CX, 5
    SUB CX, BX                                                          ; CX = ceros faltantes.
    JLE FinFormatoOk                                                    ; Si tiene 5+ decimales, terminar.

; Loop para rellenar cantidad determina de ceros. 
    MOV AL, '0'
AgregarCeroLoop:
    MOV [SI], AL
    INC SI
    LOOP AgregarCeroLoop
    MOV BYTE PTR [SI], '$'
    JMP FinFormatoOk

; Loop para rellenado completo.
AgregarPuntoYDecimales:
    MOV BYTE PTR [SI], '.'
    INC SI
    MOV CX, 5
    MOV AL, '0'
AgregarTodosCeros:
    MOV [SI], AL
    INC SI
    LOOP AgregarTodosCeros
    MOV BYTE PTR [SI], '$'

; Clear flag para caso de exito. 
FinFormatoOk:
    CLC

FinFormato:
    POP DI
    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    RET
FormatoCalif ENDP

; Verifica si +100, entonces corta. 
VerifCien PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH SI
    
    LEA SI, buffer_calif+2
    XOR CX, CX                                                          ; CX = parte entera.
    MOV BX, 10
    
; Calculo de parte entera. 
CalcularEntero:
    MOV AL, [SI]
    CMP AL, '$'
    JE  VerificarCien
    CMP AL, 13
    JE  VerificarCien
    CMP AL, '.'
    JE  VerificarCien
    
    ; Acumular parte entera.
    SUB AL, '0'                                                         ; Convertir a n√∫mero.
    MOV AH, 0
    PUSH AX
    MOV AX, CX
    MUL BX                                                              ; CX = CX * 10
    MOV CX, AX
    POP AX
    ADD CX, AX                                                          ; CX = CX + d√≠gito
    
    INC SI
    JMP CalcularEntero

VerificarCien:
    ; Verificar si la parte entera es >= 100.
    CMP CX, 100

    ; Terminar si menor...
    JL  FinVerifCien
    
    ; Cortar a '100.00000'.
    LEA SI, buffer_calif+2                                              ; Reiniciar al inicio del buffer.
    
    ; Escribir '100.00000'.
    MOV BYTE PTR [SI], '1'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '.'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '0'
    INC SI
    MOV BYTE PTR [SI], '$'
    
    ; Actualizar longitud en el buffer.
    MOV buffer_calif[1], 9

FinVerifCien:
    POP SI
    POP CX
    POP BX
    POP AX
    RET
VerifCien ENDP

; ---< Buscar Estudiante por ID >--- 
BusquedaPorID PROC
    CALL LimpiarPantalla

    ; T√≠tulo de la Secci√≥n.
    MOV AH, 09h
    LEA DX, msgT3
    INT 21h

    ; Mensaje de b√∫squeda.
    MOV AH, 09h
    LEA DX, msgPedirID
    INT 21h
    
    ; Lectura del ID ingresado. 
    MOV buffer_id, 3          ; M√°x 2 d√≠gitos (+Enter)
    LEA DX, buffer_id
    MOV AH, 0Ah              ; Lectura de Cadena
    INT 21h
    
    XOR AX, AX               ; Limpiar AX (acumulador)
    XOR CX, CX               ; Limpiar CX
    MOV BX, 10               ; Base 10
    LEA SI, buffer_id + 2    ; SI apunta al primer car√°cter

    ; Verificaci√≥n si ID nulo
    MOV CL, buffer_id + 1    ; N√∫mero de caracteres le√≠dos
    CMP CL, 0
    JE IDInvalidoBusqueda

ConversionID:
    MOV CL, [SI]             ; Lectura de caracter
    INC SI                   ; Siguiente posici√≥n
    
    CMP CL, 13               ; Verificar si es Enter (fin)
    JE ValidacionID
    
    ; Verifica que sea un d√≠gito
    CMP CL, '0'
    JL IDInvalidoBusqueda
    CMP CL, '9'
    JG IDInvalidoBusqueda
    
    ; Convertir a n√∫mero
    SUB CL, '0'              ; Conversi√≥n ASCII a n√∫mero
    MOV DX, BX               ; DX = 10
    MUL DX                   ; AX = AX * 10
    JC IDInvalidoBusqueda    ; Overflow: error
    
    ; ¬°CORRECCI√ìN CR√çTICA AQU√ç!
    ADD AL, CL               ; Sumar el nuevo d√≠gito (8 bits)
    ADC AH, 0                ; Ajustar carry si es necesario
    JC IDInvalidoBusqueda    ; Overflow: error
    
    JMP ConversionID         ; Itera hasta ENTER

ValidacionID:
    ; Validar rango
    CMP AX, 1
    JL IDInvalidoBusqueda    ; Si es menor que 1: inv√°lido
    
    ; Comparaci√≥n con l√≠mite superior
    XOR BX, BX
    MOV BL, contador
    CMP AX, BX
    JG IDInvalidoBusqueda    ; Si es mayor que contador: inv√°lido
    
    ; Convertir a √≠ndice base 0
    DEC AX
    MOV temp_index, AL       ; Guardar √≠ndice temporalmente
    
    MOV AH, 09h
    LEA DX, nueva_linea
    INT 21h

    MOV AH, 09h
    LEA DX, msgSeparador
    INT 21h

    ; Cargar √≠ndice y mostrar
    MOV AL, temp_index
    CALL MostarPorID

    MOV AH, 09h
    LEA DX, msgSeparador
    INT 21h
    
    ; Volver a men√∫
    MOV AH, 09h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h
    RET
    
IDInvalidoBusqueda:
    ; ID ingresado inv√°lido
    MOV AH, 09h
    LEA DX, msgIDInvalido
    INT 21h
    MOV AH, 09h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h
    RET
BusquedaPorID ENDP

MostarPorID PROC                                                    ; AL = √≠ndice del estudiante (base 0).
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    
    ; Comparar √≠ndice con n√∫mero de estudiantes. 
    CMP AL, contador
    JGE IDInvalidoBusqueda
    
    ; Guaradr ID en BX. 
    XOR BX, BX
    MOV BL, AL
    
    ; Mostar ID del estudiante. 
    MOV AH, 02h
    MOV DL, 09h
    INT 21h

    ; Verificar si es mayor a 9
    MOV DL, BL
    INC DL
    CMP DL, 10                                                       ; Verificar cantidad de d√≠gitos al comparar con 10. 
    JB  UnDigito

    ; Con dos d√≠gitos mostrar "1" y luego unidad.
    PUSH DX
    MOV DL, '1'
    INT 21h
    POP DX
    SUB DL, 10                                                      ; DL(unidad) = DL(√≠nidice) - 10

UnDigito:
    ADD DL, '0'                                                     ; Convertir a ASCII. 
    INT 21h

    MOV DL, '.'
    INT 21h
    MOV DL, 09h
    INT 21h
    
    ; Mostrar Nombre.
    MOV AL, BL
    MOV CL, tam_nombre + 1                                          ; CL = tama√±o (+'$').
    MUL CL                                                          ; AX (offset) = AL * CL.
    LEA SI, lista_nombres
    ADD SI, AX                                                      ; offset + inicio = destino. 
    CALL ImprimirCadena
    
    MOV AH, 02h
    MOV DL, 09h
    INT 21h
    
    ; Mostrar calificaci√≥n. 
    MOV AL, BL
    MOV CL, tam_calif + 1
    MUL CL
    LEA SI, lista_califs
    ADD SI, AX
    CALL ImprimirCadena

    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    RET
MostarPorID ENDP

; ---< Registros >--- 
MostrarListaCompleta PROC
    CALL LimpiarPantalla

    ; T√≠tulo de la Secci√≥n.
    MOV AH, 09h
    LEA DX, msgT5
    INT 21h
    LEA DX, nueva_linea
    INT 21h
    
    MOV AH, 09h
    LEA DX, msgEncabezado
    INT 21h
    LEA DX, msgSeparador
    INT 21h
    
    ; Verificar si hay estudiantes.
    MOV CL, contador
    CMP CL, 0
    JE ListaVacia
    
    ; Recorrer todos los estudiantes
    XOR CX, CX                   ; CX = 0 (contador de estudiantes)
    
MostrarEstudiante:
    ; Guardar contador
    PUSH CX
    
    ; Pasar √≠ndice a AL y llamar a MostarPorID
    MOV AL, CL
    CALL MostarPorID
    
    POP CX
    
    MOV AH, 09h
    LEA DX, nueva_linea
    INT 21h

    ; Verificar el n√∫mero de estudiantes mostrados.
    INC CL
    CMP CL, contador
    JL MostrarEstudiante
    
    JMP FinMostrar
    
ListaVacia:
    MOV AH, 09h
    LEA DX, msgListaVacia
    INT 21h

FinMostrar:
    MOV AH, 09h
    LEA DX, nueva_linea
    INT 21h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h
    RET
MostrarListaCompleta ENDP

; ---< Estad√É¬≠sticas >---
MostrarEstadisticas PROC
    CALL LimpiarPantalla 

    MOV AH, 09h
    LEA DX, msgT2
    INT 21h

    ; Verificar n√É¬∫mero de registros. 
    MOV AL, contador
    CMP AL, 0
    JNE MostrarStats
    
    ; En caso de que no de haber estudiantes.
    MOV AH, 09h
    LEA DX, msgNoStats
    INT 21h

MostrarStats:
    CALL calcular_porcentajes 
    CALL calcular_ponderado 
    RET

MostrarEstadisticas ENDP 

FinEstadisticas:
    MOV AH, 09h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h 
    RET

; ---< Ordenar Calificaciones >---
OrdenarCalif PROC
    CALL LimpiarPantalla

    ; T√É¬≠tulo de la Secci√É¬≥n.
    MOV AH, 09h
    LEA DX, msgT4
    INT 21h

    MOV AH, 09h
    LEA DX, msgContinuar
    INT 21h
    MOV AH, 01h
    INT 21h
    RET
OrdenarCalif ENDP

; ---< Funciones Auxiliares >---

; --- Copiado de buffer en lista espec√É¬≠fica --- 
CopiarCadena PROC                                                   ; SI = direcci√É¬≥n de cadena a copiar.
    PUSH AX                                                         ; DI = direcci√É¬≥n destino.
    PUSH CX
    PUSH SI
    PUSH DI
    
CopiarLoop:
    MOV AL, [SI]                                                    ; Cargar car√É¬°cter.
    CMP AL, 0Dh
    JE FinCopia                                                     ; Verificar ENTER.
    CMP AL, 0Ah
    JE SaltarChar                                                   ; Saltar al siguiente caracter. 
    CMP AL, '$'
    JE FinCopia                                                     ; Verificar terminador '$'.
    
    MOV [DI], AL                                                    ; Copiar car√É¬°cter al destino.
    INC DI                                                          ; Avanzar.

SaltarChar:            
    INC SI                                                          ; Avanzar al siguiente caracter de buffer.
    JNE CopiarLoop
    
FinCopia:
    MOV BYTE PTR [DI], '$'                                          ; Agregar terminador al final de la linea. 
    POP DI
    POP SI
    POP CX
    POP AX
    RET
CopiarCadena ENDP

; --- Imprime cadena de texto desde la SI --- 
ImprimirCadena PROC                                                 ; SI posici√É¬≥n inicial de la cadena.
    PUSH AX
    PUSH DX
    PUSH SI
    
ImprimirLoop:
    MOV DL, [SI]                                                    ; Car√É¬°cter en apuntado por SI. 
    CMP DL, '$'                                                     ; Verificar terminador.
    JE FinImprimir
    
    ; Impresi√É¬≥n de caracter y loop. 
    MOV AH, 02h
    INT 21h
    INC SI
    JMP ImprimirLoop
    
FinImprimir:
    POP SI
    POP DX
    POP AX
    RET
ImprimirCadena ENDP

; --- Limpia pantalla ---
LimpiarPantalla:
    ; Limpiado de pantalla. 
    MOV AX, 0600h                                                   ; AH=06h (scroll), AL=00h (clr).
    MOV BH, 07h                                                     ; Atributo (gris sobre negro).
    MOV CX, 0000h                                                   ; Esquina superior izquierda.
    MOV DX, 184Fh                                                   ; Esquina inferior derecha.
    INT 10h
    
    ; Posicionar cursor en 0,0.
    MOV AH, 02h
    MOV BH, 00h
    MOV DX, 0000h
    INT 10h
    RET 

; --- Separado lista de strings a listas num√É¬©ricas ---
separar_numeros_func proc
    push ax
    push bx
    push cx
    push dx
    push si
    push di
    
    ; Inicializar indices
    mov si, offset lista_califs
    mov di, offset array_enteros
    mov bx, offset array_decimales
    mov cx, 0                      ; Contador de numeros
    
procesar_numero:
    ; Reiniciar valores temporales (32 bits para decimal)
    mov word ptr temp_entero, 0
    mov word ptr temp_decimal, 0     ; Parte baja
    mov word ptr temp_decimal + 2, 0 ; Parte alta (32 bits total)
    mov flag_decimal, 0
    
leer_entero:
    mov al, [si]
    cmp al, '$'              ; Fin del string?
    je fin_numero
    cmp al, '.'              ; Es punto decimal?
    je encontro_decimal
    cmp al, 13               ; Es carriage return?
    je fin_numero
    cmp al, 10               ; Es new line?
    je fin_numero
    
    ; Convertir ASCII a numero
    sub al, '0'              ; convierte un dÌgito almacenado como car·cter ASCII en su equivalente numÈrico.
    mov ah, 0
    push ax                  ; Guardar nuevo digito
    
    ; temp_entero = temp_entero * 10
    mov ax, temp_entero
    mov dx, 10
    mul dx
    mov temp_entero, ax
    
    ; temp_entero = temp_entero + nuevo_digito
    pop ax
    add temp_entero, ax
    
    inc si
    jmp leer_entero

encontro_decimal:
    mov flag_decimal, 1
    inc si                   ; Saltar el punto
    
leer_decimal:
    mov al, [si]
    cmp al, '$'              ; Fin del string?
    je fin_numero
    cmp al, 13               ; Es carriage return?
    je fin_numero
    cmp al, 10               ; Es new line?
    je fin_numero
    
    ; Convertir ASCII a n√Ø¬ø≈ìmero
    sub al, '0'
    mov ah, 0
    push ax                  ; Guardar nuevo d√Ø¬ø≈ìgito
    
    ; temp_decimal = temp_decimal * 10 (32 bits)
    push bx
    push cx
    push dx
    
    ; Multiplicar parte baja (temp_decimal) por 10
    mov ax, word ptr temp_decimal
    mov dx, 10
    mul dx
    mov word ptr temp_decimal, ax
    mov cx, dx              ; Guardar carry
    
    ; Multiplicar parte alta (temp_decimal + 2) por 10 y sumar carry
    mov ax, word ptr temp_decimal + 2
    mov dx, 10
    mul dx
    add ax, cx              ; Sumar el carry de la parte baja
    mov word ptr temp_decimal + 2, ax
    
    pop dx
    pop cx
    pop bx
    
    ; temp_decimal = temp_decimal + nuevo_d√Ø¬ø≈ìgito
    pop ax
    add word ptr temp_decimal, ax
    adc word ptr temp_decimal + 2, 0  ; Sumar carry si hay
    
    inc si
    jmp leer_decimal

fin_numero:
    ; Guardar entero en array (16 bits)
    mov ax, temp_entero
    mov [di], ax
    add di, 2
    
    ; Guardar decimal en array (32 bits - 2 palabras)
    mov ax, word ptr temp_decimal      ; Parte baja
    mov [bx], ax
    mov ax, word ptr temp_decimal + 2  ; Parte alta
    mov [bx + 2], ax
    add bx, 4                         ; Avanzar 4 bytes (32 bits)
    
    ; Avanzar al siguiente string en lista_califs
    inc cx
    mov al, contador
    cbw
    cmp cx, ax               ; Comparar con contador (convertido a word)
    jge terminar_proceso     ; Si ya procesamos todos, terminar
    
    ; Avanzar SI al inicio del siguiente string (11 bytes por elemento)
    ; Calcular: SI = offset lista_califs + (cx * 11)
    push ax
    push dx
    mov ax, cx               ; AX = numero actual (1, 2, 3...)
    mov dx, tam_calif
    inc dx                   ; DX = 11 (tam_calif + 1)
    mul dx                   ; AX = cx * 11
    mov si, offset lista_califs
    add si, ax               ; SI apunta al inicio del siguiente string
    pop dx
    pop ax
    
    jmp procesar_numero      ; Procesar el siguiente n√Ø¬ø≈ìmero
    
terminar_proceso:
    pop di
    pop si
    pop dx
    pop cx
    pop bx
    pop ax
    ret
separar_numeros_func endp


; --- Calcular Porcentajes de Estudiantes ---
calcular_porcentajes proc
    mov cl, contador        ; contador es 8 bits, usamos CL como contador
    xor si, si         ; Reinicio source index
    mov contador_aprobados,0
    mov contador_desaprobados,0

ciclo_notas:
    cmp cl,0
    je fin_ciclo

    mov ax, array_enteros[si] ; cargar nota
    cmp ax,70
    jl es_reprobado

es_aprobado:
    inc contador_aprobados
    jmp siguiente

es_reprobado:
    inc contador_desaprobados

siguiente:
    add si,2       ; siguiente palabra
    dec cl
    jmp ciclo_notas

fin_ciclo:

    ; ------------------------------
    ; porcentaje contador_aprobados = (contador_aprobados * 100) / contador
    ; ------------------------------
    xor ax, ax        ; LIMPIAR AX COMPLETAMENTE
    mov al, contador_aprobados ; cargar contador_aprobados (8 bits)
    mov bl, 100
    mul bl            ; AX = contador_aprobados * 100
    mov bl, contador       ; divisor
    div bl            ; AL = cociente, AH = residuo
    xor ah, ah        ; descartar residuo, AX = porcentaje
    push ax           ; guardar porcentaje

    ; imprimir mensaje de contador_aprobados
    mov ah, 9
    lea dx, msgPorcAprobados
    int 21h
    pop ax
    call print_num    ; imprimir porcentaje

    ; ------------------------------
    ; porcentaje reprobados = (contador_desaprobados * 100) / contador
    ; ------------------------------
    xor ax, ax        ; LIMPIAR AX COMPLETAMENTE - ESTO ES LO QUE FALtabuladorA
    mov al, contador_desaprobados ; cargar contador_desaprobados (8 bits)
    mov bl, 100
    mul bl            ; AX = contador_desaprobados * 100
    mov bl, contador       ; divisor
    div bl            ; AL = cociente, AH = residuo
    xor ah, ah        ; descartar residuo, AX = porcentaje

    ; imprimir mensaje de reprobados
    push ax           ; guardar porcentaje temporalmente
    mov ah, 9
    lea dx, msgPorcReprobados
    int 21h
    pop ax
    call print_num

    ret
calcular_porcentajes endp

;---------------------------------
; Imprimir Numero contenido en AX
;---------------------------------
print_num proc
    push ax
    push bx
    push cx
    push dx

    mov cx, 0          ; contador de digitos
    mov bx, 10         ; divisor para decimal

    cmp ax, 0
    jne conv_loop
    ; si AX=0, imprimir '0' directamente
    mov dl, '0'
    mov ah, 2
    int 21h
    jmp print_symbol

conv_loop:
    xor dx, dx
    div bx            ; AX / 10 -> cociente en AX, residuo en DX
    push dx           ; guardar residuo (digito)
    inc cx
    cmp ax, 0
    jne conv_loop

print_digits:
    pop dx
    add dl, '0'
    mov ah, 2
    int 21h
    loop print_digits

print_symbol:
    ; imprimir simbolo de porcentaje
    mov dl, '%'
    mov ah, 2
    int 21h

    pop dx
    pop cx
    pop bx
    pop ax
    ret

print_num endp

;---------------------------------
; BUBBLE_SORT_INDICES
;---------------------------------
                 
BUBBLE_SORT_INDICES PROC
    ; Cargar contador y verificar
    mov al, contador
    mov ah, 0
    mov cx, ax
    cmp cx, 0
    je FIN_SORT
    cmp cx, 1
    je FIN_SORT

    JMP SEGUIR_BS

FIN_SORT:
    ret

SEGUIR_BS:    
    ; Inicializar array_BS con √≠ndices secuenciales
    mov si, 0
    mov ax, 0
    
    ; Al final array_BS queda con los Ìndices 0, 1, 2, 3, 4, .... , contador - 1 
    ; en palabras de 16 bits, listos para usarse en ordenamientos o recorridos.
INICIALIZAR_INDICES:
    mov array_BS[si], ax
    add si, 2
    inc ax
    loop INICIALIZAR_INDICES

    ; Bubble sort
    mov al, contador
    mov ah, 0
    mov cx, ax
    dec cx
    
EXTERNO_LOOP:
    push cx
    mov si, 0
    
    mov al, contador
    mov ah, 0
    mov cx, ax
    dec cx
    
INTERNO_LOOP:
    mov ax, array_BS[si]  ; Primer par de bytes
    mov bx, array_BS[si+2]; Segundo par de bytes
    
    ; Comparar partes enteras
    mov di, ax
    shl di, 1             ; significa: "Shift Left" (desplazar a la izquierda) el registro DI una vez.
    mov dx, array_enteros[di]
    
    mov di, bx
    shl di, 1
    mov bp, array_enteros[di]
    
    cmp orden, 0
    je ORDEN_ASC
    
    ; ========== ORDEN DESCENDENTE ==========
    cmp dx, bp
    jl NO_SWAP                ; entero[i] < entero[j] ? OK
    jg SWAP_INDICES           ; entero[i] > entero[j] ? SWAP
    ; si iguales ? comparar decimales en DSC
    call COMPARE_DECIMALES
    cmp resultado_dec, 1      ; i.dec > j.dec
    je NO_SWAP
    cmp resultado_dec, 0      ; i.dec < j.dec
    je SWAP_INDICES
    jmp NO_SWAP               ; si iguales ? no swap
    jmp CONTINUAR

ORDEN_ASC:
    ; ========== ORDEN ASCENDENTE ==========
    cmp dx, bp
    jg NO_SWAP                ; entero[i] > entero[j] ? OK
    jl SWAP_INDICES           ; entero[i] < entero[j] ? SWAP
    ; si iguales ? comparar decimales en ASC
    call COMPARE_DECIMALES
    cmp resultado_dec, 1      ; i.dec > j.dec
    je SWAP_INDICES
    cmp resultado_dec, 0      ; i.dec < j.dec
    je NO_SWAP
    jmp NO_SWAP               ; si iguales ? no swap

SWAP_INDICES:
    mov ax, array_BS[si]
    mov bx, array_BS[si+2]
    mov array_BS[si], bx
    mov array_BS[si+2], ax

NO_SWAP:

CONTINUAR:
    add si, 2
    loop INTERNO_LOOP
    
    pop cx
    loop EXTERNO_LOOP
    JMP FIN_SORT
BUBBLE_SORT_INDICES ENDP


;-----------------------------------
; Pregunar por orden de Bubble Sort
;-----------------------------------

PREGUNTAR_ORDEN PROC
PREGUNTAR:
    ; Mostrar mensaje de pregunta
    mov ah, 09h
    lea dx, msg_pregunta
    int 21h
    
    ; Leer un solo car√°cter
    mov ah, 01h
    int 21h
    
    ; Verificar entrada
    cmp al, '1'
    je ES_ASC
    cmp al, '0'
    je ES_DSC
    jmp ERROR_INPUT

ES_ASC:
    mov orden, 1               ; ASC = 1
    jmp FIN_PREGUNTA

ES_DSC:
    mov orden, 0               ; DSC = 0
    jmp FIN_PREGUNTA

ERROR_INPUT:
    ; Mostrar mensaje de error
    mov ah, 09h
    lea dx, msg_error
    int 21h
    jmp PREGUNTAR

FIN_PREGUNTA:
    ret
PREGUNTAR_ORDEN ENDP   


;---------------------------------
; ordenarListas 
;---------------------------------
ordenarListas PROC
    push ax
    push bx
    push cx
    push dx
    push si
    push di
    push es
    
    push ds
    pop es                   ; ES = DS
    
    ; PRIMERO: COPIAR TODO A BUFFERS (respaldo)
    mov cl, contador
    mov ch, 0
    mov ax, tam_nombre + 1
    mul cx                                ; AX contiene (tamaÒo + 1) * contador
    mov cx, ax
    mov si, offset lista_nombres           ; Source index inicia en la lista de nombres
    mov di, offset buffer_nombres_ordenados; Destination index inicia en buffer de nombres ordenados
    rep movsb  ; Cambio de bloques de memoria.
    
    mov cl, contador
    mov ch, 0
    mov ax, tam_calif + 1
    mul cx                                 ; AX contiene (tamaÒo + 1) * contador
    mov cx, ax
    mov si, offset lista_califs
    mov di, offset buffer_califs_ordenados
    rep movsb
    
    ; SEGUNDO: REORDENAR COPIANDO DEL BUFFER AL ORIGINAL
    mov cl, contador
    mov ch, 0
    mov si, 0                ; SI = √≠ndice para array_BS
    
reordenar:
    mov bx, [array_BS + si]  ; BX = √≠ndice original
    and bx, 00FFh            ; Asegurar 8 bits
    
    ; === COPIAR NOMBRE ===
    ; DI = destino en lista original
    mov ax, si               ; AX = posici√≥n en array_BS
    shr ax, 1                ; AX = nueva posici√≥n (0, 1, 2...)
    mov dl, tam_nombre + 1
    mul dl
    mov di, ax
    add di, offset lista_nombres
    
    ; Calcular origen en buffer (usar DX como temporal)
    mov ax, bx               ; AX = √≠ndice original
    mov dl, tam_nombre + 1
    mul dl
    mov temp_reg, ax         ; Usar variable temporal
    mov dx, temp_reg
    add dx, offset buffer_nombres_ordenados
    mov source_ptr, dx       ; Guardar en variable
    
    ; Copiar nombre
    push cx
    push si
    mov cx, tam_nombre + 1
    mov si, source_ptr       ; SI = origen desde variable
    rep movsb                ; BUFFER -> ORIGINAL
    pop si
    pop cx
    
    ; === COPIAR CALIFICACI√ìN ===
    ; DI = destino en lista original
    mov ax, si               ; AX = posici√≥n en array_BS
    shr ax, 1                ; AX = nueva posici√≥n
    mov dl, tam_calif + 1
    mul dl
    mov di, ax
    add di, offset lista_califs
    
    ; Calcular origen en buffer (usar DX como temporal)
    mov ax, bx               ; AX = √≠ndice original
    mov dl, tam_calif + 1
    mul dl
    mov temp_reg, ax         ; Usar variable temporal
    mov dx, temp_reg
    add dx, offset buffer_califs_ordenados
    mov source_ptr, dx       ; Guardar en variable
    
    ; Copiar calificaci√≥n
    push cx
    push si
    mov cx, tam_calif + 1
    mov si, source_ptr       ; SI = origen desde variable
    rep movsb                ; BUFFER -> ORIGINAL
    pop si
    pop cx
    
    add si, 2                ; Siguiente en array_BS
    loop reordenar
    
    pop es
    pop di
    pop si
    pop dx
    pop cx
    pop bx
    pop ax
    ret
ordenarListas ENDP 


;------------------------------------
; Generar Maximo y Minimo --> Usa BS
;------------------------------------

generar_Max_Min PROC   
    mov al, orden
    cmp al, 1
    je  desc
    cmp al, 0
    je  asce 
    RET

generar_Max_Min ENDP

desc:
    ;Datos organizados de forma descendente
    
    ;Primer dato es el maximo
    mov ah, 9
    lea dx, msgMaxNota
    int 21h
    
    mov al, contador
    dec al
    CALL MostarPorID 
    
    ;Ultimo dato es el minimo
    mov ah, 9
    lea dx, msgMinNota
    int 21h
    
    mov al, 1
    dec al
    CALL MostarPorID 
    RET
asce:
    ;Datos organizados de forma ascendente 
    
    ;Primer dato es el minimo 
    mov ah, 9
    lea dx, msgMinNota
    int 21h
    
    mov al, contador
    dec al
    CALL MostarPorID 
    
    ;Ultimo dato es el maximo
    mov ah, 9
    lea dx, msgMaxNota
    int 21h
    
    mov al, 1  
    dec al
    CALL MostarPorID
    RET

;----------------------------------------
; Compara decimales                                          
;----------------------------------------  
; AX debe contener Ìndice del elemento tal que el siguiente es con quien se debe compara

COMPARE_DECIMALES PROC
    push ax
    push bx
    push cx
    push dx
    push si
    push di

    ; -------------------------
    ; Obtener √≠ndices desde array_BS
    ; -------------------------
    mov ax, array_BS[si]      ; √≠ndice i
    mov bx, array_BS[si+2]    ; √≠ndice j

    ; -------------------------
    ; Calcular offset decimal[i]
    ; -------------------------
    mov dx, ax        ; dx = i
    shl dx, 1
    shl dx, 1         ; dx = i*4
    mov di, dx        ; di = offset de decimal[i]

    mov cx, array_decimales[di]      ; parte baja 
    mov dx, array_decimales[di+2]    ; parte alta
    ; -------------------------
    ; Calcular offset decimal[j]
    ; -------------------------
    mov bx, bx        ; bx = j
    shl bx, 1
    shl bx, 1         ; bx = j*4 ? offset j

    mov si, array_decimales[bx]      ; parte baja
    mov bp, array_decimales[bx+2]    ; parte alta
                                                   
    ; -------------------------
    ; Comparar parte alta
    ; -------------------------
    cmp dx, bp
    ja  DEC_MAYOR
    jb  DEC_MENOR

    ; Si iguales, comparar parte baja
    cmp cx, si
    ja  DEC_MAYOR
    jb  DEC_MENOR

    ; Iguales
    mov resultado_dec, 2
    jmp FIN_COMPARE

DEC_MAYOR:
    mov resultado_dec, 0
    jmp FIN_COMPARE

DEC_MENOR:
    mov resultado_dec, 1

FIN_COMPARE:
    pop di
    pop si
    pop dx
    pop cx
    pop bx
    pop ax
    ret
COMPARE_DECIMALES ENDP


; --------------------------------------
; Calcular Promedio
; --------------------------------------
calcular_ponderado PROC
    MOV CL, contador
    CMP CL, 0
    JE fin_calculo_ponderado
    
    XOR SI, SI              ; Reiniciar Ìndice
    XOR AX, AX              ; Reiniciar acumulador
    MOV suma_total, 0
    
ciclo_suma:
    MOV BX, array_enteros[SI] ; Obtener calificaciÛn entera
    ADD AX, BX              ; Sumar al acumulador
    ADD SI, 2               ; Siguiente elemento
    LOOP ciclo_suma
    
    MOV suma_total, AX      ; Guardar suma total
    
    ; Calcular promedio (suma_total / n˙mero_estudiantes)
    XOR DX, DX
    MOV BX, 0
    MOV BL, contador
    DIV BX                  ; AX = suma_total / contador
    MOV promedio_ponderado, AX
    
    ; Mostrar resultado
    MOV AH, 09h
    LEA DX, msgPonderado
    INT 21h
    
    MOV AX, promedio_ponderado
    CALL print_promedio    
    
fin_calculo_ponderado:
    RET
calcular_ponderado ENDP 

;--------------------
; Printear numero
;--------------------
print_promedio PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX

    MOV CX, 0          ; contador de dÌgitos
    MOV BX, 10         ; divisor para decimal

    ; Caso especial: si AX = 0, imprimir '0'
    CMP AX, 0
    JNE conv_loop_sin
    MOV DL, '0'
    MOV AH, 02h
    INT 21h
    JMP fin_print_sin

conv_loop_sin:
    XOR DX, DX
    DIV BX            ; AX / 10 ? cociente en AL, residuo en AH 
    PUSH DX           ; guardar residuo (dÌgito)
    INC CX
    CMP AX, 0
    JNE conv_loop_sin

print_digit_sin:
    POP DX
    ADD DL, '0'       ; convertir dÌgito a ASCII
    MOV AH, 02h
    INT 21h
    LOOP print_digit_sin

fin_print_sin:
    POP DX
    POP CX
    POP BX
    POP AX
    RET
print_promedio ENDP






END START